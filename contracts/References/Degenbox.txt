// SPDX-License-Identifier: MIT

pragma solidity 0.6.12;
pragma experimental ABIEncoderV2;

import "@boringcrypto/boring-solidity/contracts/libraries/BoringMath.sol";
import "@boringcrypto/boring-solidity/contracts/BoringBatchable.sol";
import "@boringcrypto/boring-solidity/contracts/BoringOwnable.sol";
import "@boringcrypto/boring-solidity/contracts/libraries/BoringERC20.sol";
import "./libraries/SignedSafeMath.sol";
import "hardhat/console.sol";

contract StakingContract is BoringOwnable, BoringBatchable {
    using BoringMath for uint256;
    using BoringMath128 for uint128;
    using BoringERC20 for IERC20;
    using SignedSafeMath for int256;

    /// @notice Info of each MCV2 user.
    /// `amount` LP token amount the user has provided.
    /// `rewardDebt` The amount of TOKEN entitled to the user.
    struct UserInfo {
        uint256 amount;
        int256 rewardDebt;
    }

    /// @notice Info of each MCV2 pool.
    /// `allocPoint` The amount of allocation points assigned to the pool.
    /// Also known as the amount of TOKEN to distribute per block.
    struct PoolInfo {
        uint128 accTokenPerShare;
        uint64 lastRewardBlock;
        uint64 allocPoint;
        uint256 maxTokenPerStaker;
        uint256 tokenPerBlock;
        IERC20 TOKEN;
    }

    /// @notice Address of TOKEN for each MCV2 pool.
    IERC20[] public TOKEN;

    /// @notice Info of each MCV2 pool.
    PoolInfo[] public poolInfo;

    /// @notice Address of the LP token for each MCV2 pool.
    IERC20[] public lpToken;

    /// @notice Info of each user that stakes LP tokens.
    mapping(uint256 => mapping(address => UserInfo)) public userInfo;

    /// @notice Info of each value of each reward tokens
    mapping(address => uint256) public tokenPerBlock;

    /// @dev Total allocation points. Must be the sum of all allocation points in all pools.
    uint256 public totalAllocPoint;

    uint256 private constant ACC_TOKEN_PRECISION = 1e12;

    event AddTokenEmission(address tokenAddress, uint256 amount);

    event Deposit(
        address indexed user,
        uint256 indexed pid,
        uint256 amount,
        address indexed to
    );
    event Withdraw(
        address indexed user,
        uint256 indexed pid,
        uint256 amount,
        address indexed to
    );
    event EmergencyWithdraw(
        address indexed user,
        uint256 indexed pid,
        uint256 amount,
        address indexed to
    );
    event Harvest(address indexed user, uint256 indexed pid, uint256 amount);
    event LogPoolAddition(
        uint256 indexed pid,
        uint256 allocPoint,
        IERC20 indexed lpToken,
        uint256 maxTokenPerStaker,
        IERC20 indexed token
    );
    event LogSetPool(uint256 indexed pid, uint256 allocPoint);
    event LogUpdatePool(
        uint256 indexed pid,
        uint64 lastRewardBlock,
        uint256 lpSupply,
        uint256 accTokenPerShare
    );
    event LogInit();
    event TokenPerBlockUpdated(uint256 tokenPerBlock);

    /// @notice Returns the number of MCV2 pools.
    function poolLength() public view returns (uint256 pools) {
        pools = poolInfo.length;
    }

    /// @notice Add token per block. Can only be called by the owner.
    /// @param _tokenAddress token address.
    /// @param _amount emission amount of the token.
    function addTokenEmission(
        address _tokenAddress,
        uint256 _amount
    ) public onlyOwner {
        require(_tokenAddress != address(0), "Invalid Address");
        require(_amount > 0, "Amount cannot be empty or zero");
        require(
            tokenPerBlock[_tokenAddress] == 0,
            "Token emission already added"
        );
        tokenPerBlock[_tokenAddress] = _amount;
        emit AddTokenEmission(_tokenAddress, _amount);
    }

    /// @notice Add a new LP to the pool. Can only be called by the owner.
    /// DO NOT add the same LP token more than once. Rewards will be messed up if you do.
    /// @param allocPoint AP of the new pool.
    /// @param _lpToken Address of the LP ERC-20 token.
    function add(
        uint256 allocPoint,
        IERC20 _lpToken,
        uint256 _maxTokenPerStaker,
        IERC20 _token
    ) public onlyOwner {
        uint256 emissionPerBlock = tokenPerBlock[address(_token)];
        require(emissionPerBlock > 0, "Token per block not found!");
        require(address(_lpToken) != address(0), "Invalid LpToken Address");
        require(address(_token) != address(0), "Invalid Token Address");

        uint256 lastRewardBlock = block.number;
        totalAllocPoint = totalAllocPoint.add(allocPoint);
        lpToken.push(_lpToken);
        TOKEN.push(_token);

        poolInfo.push(
            PoolInfo({
                allocPoint: allocPoint.to64(),
                lastRewardBlock: lastRewardBlock.to64(),
                accTokenPerShare: 0,
                maxTokenPerStaker: _maxTokenPerStaker,
                tokenPerBlock: emissionPerBlock,
                TOKEN: _token
            })
        );
        emit LogPoolAddition(
            lpToken.length.sub(1),
            allocPoint,
            _lpToken,
            _maxTokenPerStaker,
            _token
        );
    }

    /// @notice Update the given pool's TOKEN allocation point contract. Can only be called by the owner.
    /// @param _pid The index of the pool. See `poolInfo`.
    /// @param _allocPoint New AP of the pool.
    function set(uint256 _pid, uint256 _allocPoint) public onlyOwner {
        totalAllocPoint = totalAllocPoint.sub(poolInfo[_pid].allocPoint).add(
            _allocPoint
        );
        poolInfo[_pid].allocPoint = _allocPoint.to64();
        emit LogSetPool(_pid, _allocPoint);
    }

    /// @notice View function to see pending TOKEN on frontend.
    /// @param _pid The index of the pool. See `poolInfo`.
    /// @param _user Address of user.
    /// @return pending TOKEN reward for a given user.
    function pendingToken(
        uint256 _pid,
        address _user
    ) external view returns (uint256 pending) {
        PoolInfo memory pool = poolInfo[_pid];
        UserInfo storage user = userInfo[_pid][_user];
        uint256 accTokenPerShare = pool.accTokenPerShare;
        uint256 lpSupply = lpToken[_pid].balanceOf(address(this));
        if (block.number > pool.lastRewardBlock && lpSupply != 0) {
            uint256 blocks = block.number.sub(pool.lastRewardBlock);
            uint256 tokenReward = blocks.mul(pool.tokenPerBlock).mul(
                pool.allocPoint
            ) / totalAllocPoint;
            accTokenPerShare = accTokenPerShare.add(
                tokenReward.mul(ACC_TOKEN_PRECISION) / lpSupply
            );
        }
        pending = int256(
            user.amount.mul(accTokenPerShare) / ACC_TOKEN_PRECISION
        ).sub(user.rewardDebt).toUInt256();
    }

    /// @notice Update reward variables for all pools. Be careful of gas spending!
    /// @param pids Pool IDs of all to be updated. Make sure to update all active pools.
    function massUpdatePools(uint256[] calldata pids) external {
        uint256 len = pids.length;
        for (uint256 i = 0; i < len; ++i) {
            updatePool(pids[i]);
        }
    }

    /// @notice Update reward variables of the given pool.
    /// @param pid The index of the pool. See `poolInfo`.
    /// @return pool Returns the pool that was updated.
    function updatePool(uint256 pid) public returns (PoolInfo memory pool) {
        pool = poolInfo[pid];
        if (block.number > pool.lastRewardBlock) {
            uint256 lpSupply = lpToken[pid].balanceOf(address(this));
            if (lpSupply > 0) {
                uint256 blocks = block.number.sub(pool.lastRewardBlock);
                uint256 tokenReward = blocks.mul(pool.tokenPerBlock).mul(
                    pool.allocPoint
                ) / totalAllocPoint;
                pool.accTokenPerShare = pool.accTokenPerShare.add(
                    (tokenReward.mul(ACC_TOKEN_PRECISION) / lpSupply).to128()
                );
            }
            pool.lastRewardBlock = block.number.to64();
            poolInfo[pid] = pool;
            emit LogUpdatePool(
                pid,
                pool.lastRewardBlock,
                lpSupply,
                pool.accTokenPerShare
            );
        }
    }

    /// @notice Deposit LP tokens to MCV2 for TOKEN allocation.
    /// @param pid The index of the pool. See `poolInfo`.
    /// @param amount LP token amount to deposit.
    /// @param to The receiver of `amount` deposit benefit.
    function deposit(uint256 pid, uint256 amount, address to) public {
        PoolInfo memory pool = updatePool(pid);
        UserInfo storage user = userInfo[pid][to];
        // Checking to limit user's staking
        require(
            user.amount + amount <= pool.maxTokenPerStaker,
            "Exceeded maximum allowed deposited token"
        );
        user.amount = user.amount.add(amount);
        user.rewardDebt = user.rewardDebt.add(
            int256(amount.mul(pool.accTokenPerShare) / ACC_TOKEN_PRECISION)
        );
        lpToken[pid].safeTransferFrom(msg.sender, address(this), amount);
        emit Deposit(msg.sender, pid, amount, to);
    }

    /// @notice Withdraw LP tokens from MCV2.
    /// @param pid The index of the pool. See `poolInfo`.
    /// @param amount LP token amount to withdraw.
    /// @param to Receiver of the LP tokens.
    function withdraw(uint256 pid, uint256 amount, address to) public {
        PoolInfo memory pool = updatePool(pid);
        UserInfo storage user = userInfo[pid][msg.sender];
        user.rewardDebt = user.rewardDebt.sub(
            int256(amount.mul(pool.accTokenPerShare) / ACC_TOKEN_PRECISION)
        );
        user.amount = user.amount.sub(amount);
        lpToken[pid].safeTransfer(to, amount);
        emit Withdraw(msg.sender, pid, amount, to);
    }

    /// @notice Harvest proceeds for transaction sender to `to`.
    /// @param pid The index of the pool. See `poolInfo`.
    /// @param to Receiver of TOKEN rewards.
    function harvest(uint256 pid, address to) public {
        PoolInfo memory pool = updatePool(pid);
        UserInfo storage user = userInfo[pid][msg.sender];
        int256 accumulatedToken = int256(
            user.amount.mul(pool.accTokenPerShare) / ACC_TOKEN_PRECISION
        );
        uint256 _pendingToken = accumulatedToken
            .sub(user.rewardDebt)
            .toUInt256();
        user.rewardDebt = accumulatedToken;
        // Interactions
        if (_pendingToken != 0) {
            TOKEN[pid].safeTransfer(to, _pendingToken);
        }
        emit Harvest(msg.sender, pid, _pendingToken);
    }

    /// @notice Withdraw LP tokens from MCV2 and harvest proceeds for transaction sender to `to`.
    /// @param pid The index of the pool. See `poolInfo`.
    /// @param amount LP token amount to withdraw.
    /// @param to Receiver of the LP tokens and TOKEN rewards.
    function withdrawAndHarvest(
        uint256 pid,
        uint256 amount,
        address to
    ) public {
        PoolInfo memory pool = updatePool(pid);
        UserInfo storage user = userInfo[pid][msg.sender];
        int256 accumulatedToken = int256(
            user.amount.mul(pool.accTokenPerShare) / ACC_TOKEN_PRECISION
        );
        uint256 _pendingToken = accumulatedToken
            .sub(user.rewardDebt)
            .toUInt256();
        user.rewardDebt = accumulatedToken.sub(
            int256(amount.mul(pool.accTokenPerShare) / ACC_TOKEN_PRECISION)
        );
        user.amount = user.amount.sub(amount);
        TOKEN[pid].safeTransfer(to, _pendingToken);
        lpToken[pid].safeTransfer(to, amount);
        emit Withdraw(msg.sender, pid, amount, to);
        emit Harvest(msg.sender, pid, _pendingToken);
    }

    /// @notice Withdraw without caring about rewards. EMERGENCY ONLY.
    /// @param pid The index of the pool. See `poolInfo`.
    /// @param to Receiver of the LP tokens.
    function emergencyWithdraw(uint256 pid, address to) public {
        UserInfo storage user = userInfo[pid][msg.sender];
        uint256 amount = user.amount;
        user.amount = 0;
        user.rewardDebt = 0;

        // Note: transfer can fail or succeed if `amount` is zero.
        lpToken[pid].safeTransfer(to, amount);
        emit EmergencyWithdraw(msg.sender, pid, amount, to);
    }
}
